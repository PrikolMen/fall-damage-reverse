DMG_FALL = DMG_FALL
:TraceHull = util
:abs, :min = math
Vector = Vector
:band = bit

hook.Add "EntityTakeDamage", "Fall Damage Reverse", ( damageInfo ) =>
    unless @IsPlayer! and band( damageInfo\GetDamageType!, DMG_FALL ) == DMG_FALL and damageInfo\GetAttacker! == @
        return

    start, mins, maxs = @GetPos!, @GetHull!
    traceResult = TraceHull( {
        endpos: start - Vector( 0, 0, maxs[ 3 ] )
        filter: @
        :start
        :mins
        :maxs
    } )

    unless traceResult.Hit
        return

    entity = traceResult.Entity
    if not entity\IsValid! or entity\GetMaxHealth! <= 1
        return

    velocity = @GetVelocity!
    @SetVelocity -velocity / 3
    entity\TakeDamageInfo( damageInfo )

    fraction = min( 1, abs( velocity[ 3 ] ) / 3500 )
    util.ScreenShake( entity\WorldSpaceCenter!, 15, 150, 0.25 + 1 * fraction, 128 + 256 * fraction )

    return true